ext {
    buildVersions = [
            minSdkVersion: 16
    ]

    androidXVersion = '1.2.0'
    androidWorkVersion = '2.5.0'
    firebaseMessagingVersion = '[19.0.0, 22.99.99]'
    googleGMSVersion = '[17.0.0, 17.99.99]'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.buildVersions.compileSdkVersion
    defaultConfig {
        minSdkVersion buildVersions.minSdkVersion
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    buildTypes {
        original {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
        }
//        unity {
//            minifyEnabled false
//        }
    }
}


android.libraryVariants.all {
    variant ->

        variant.outputs.all{

            def libaryName = rootProject.name
            def type = "debug"
            def version = "4.4.0_hotfix"
            def log = getGitCommit()
            if (variant.buildType.name.equals('release')) {
                type = "release"
            }

            def fileName = libaryName + "-" +  version + "-"+ type + "-" + log  +".aar"
            outputFileName = fileName
        }
}

def getGitCommit() {
    def gitDir = new File("${new File("${rootDir}").getParentFile().getAbsolutePath()}/.git")
    if (!gitDir.isDirectory()) {
        return 'non-git-build'
    }

    def cmd = 'git log --pretty=format:%h -1'
    def gitCommit = cmd.execute().text.trim()
    assert !gitCommit.isEmpty()
    gitCommit
}

// api || implementation = compile and runtime

// KEEP: version ranges, these get used in the released POM file for maven central
dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // play-services-location:16.0.0 is the last version before going to AndroidX
    // play-services-location:17.0.0 is the first version using AndroidX
    compileOnly "com.google.android.gms:play-services-location:$googleGMSVersion"

    // play-services-base:16.1.0 is the last version before going to AndroidX
    // play-services-base:17.0.0 is the first version using AndroidX
    // Required for GoogleApiAvailability
    implementation "com.google.android.gms:play-services-base:$googleGMSVersion"

    // firebase-messaging:18.0.0 is the last version before going to AndroidX
    // firebase-messaging:19.0.0 is the first version using AndroidX
    api "com.google.firebase:firebase-messaging:$firebaseMessagingVersion"

    // Huawei PushKit
    // KEEP as "compileOnly", so OneSignal isn't a direct dependency in the POM file.
    compileOnly "com.huawei.hms:push:$huaweiHMSPushVersion"
    compileOnly "com.huawei.hms:location:$huaweiHMSLocationVersion"

    api "androidx.cardview:cardview:1.0.0"
    api "androidx.legacy:legacy-support-v4:1.0.0"
    api "androidx.browser:browser:1.3.0"
    api "androidx.appcompat:appcompat:1.2.0"
    api "androidx.work:work-runtime:$androidWorkVersion"
}

apply from: 'maven-push.gradle'